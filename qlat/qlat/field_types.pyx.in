# cython: binding=True, embedsignature=True, c_string_type=unicode, c_string_encoding=utf8

{{py:
type_name_list = [
    'ColorMatrix',
    'WilsonMatrix',
    'NonRelWilsonMatrix',
    'IsospinMatrix',
    'SpinMatrix',
    'WilsonVector',
    'ComplexD',
    'ComplexF',
    'RealD',
    'RealF',
    'Long',
    'Int',
    'Int64t',
    'Int32t',
    'Int8t',
    'Char',
    ]
}}

from qlat_utils.all cimport *
from . cimport everything as cc
from cpython cimport Py_buffer
from cpython.buffer cimport PyBUF_FORMAT
from .geometry cimport *
from .field_selection cimport *
from .fields_io cimport *
from .selected_field_types cimport *
from .selected_points_types cimport *

import qlat_utils as q
import numpy as np

from .field_type_dict import field_type_dict

### -------------------------------------------------------------------

{{for name in type_name_list}}

cdef void set_field_f_sp_{{name}}(Field{{name}} self, SelectedPoints{{name}} other):
    cc.set_field_selected(self.xx, other.xx, other.psel.geo.xx, other.psel.xx)

cdef void set_field_f_sf_{{name}}(Field{{name}} self, SelectedField{{name}} other):
    cc.set_field_selected(self.xx, other.xx, other.fsel.xx)

def set_field_f_f_{{name}}(Field{{name}} self, Field{{name}} other):
    cc.assign_direct(self.xx, other.xx)
    # self.xx = other.xx

cdef class Field{{name}}(FieldBase):

    ctype = ElemType{{name}}

    def __cinit__(self):
        self.cdata = <cc.Long>&(self.xx)
        self.view_count = 0

    def __init__(self, Geometry geo=None, int multiplicity=0):
        if geo is None:
            self.xx.init()
            return
        if self.view_count > 0:
            raise ValueError("can't re-init while being viewed")
        self.xx.init(geo.xx, multiplicity)

    def __getbuffer__(self, Py_buffer *buffer, int flags):
        cdef int ndim = 2 + ElemType{{name}}.ndim()
        cdef Buffer buf = Buffer(self, ndim)
        buf.format = ElemType{{name}}.format()
        buf.itemsize = ElemType{{name}}.itemsize()
        buf.buf = <char*>(self.xx.field.data())
        cdef int multiplicity = self.xx.get_geo().multiplicity
        buf.set_dim_size(0, self.xx.field.size() // multiplicity)
        buf.set_dim_size(1, multiplicity)
        cdef cc.std_vector[Py_ssize_t] vec = ElemType{{name}}.shape()
        cdef int dim
        for dim in range(vec.size()):
            buf.set_dim_size(2 + dim, vec[dim])
        buf.update_strides_from_shape()
        buf.set_buffer(buffer, flags)
        self.view_count += 1

    def release_buffer(self, Buffer buf):
        assert buf.obj is self
        self.view_count -= 1

    def __imatmul__(self, other):
        """
        other can be Field, SelectedField, SelectedPoints
        field geo does not change if already initialized
        """
        if isinstance(other, Field{{name}}):
            set_field_f_f_{{name}}(self, <Field{{name}}>other)
        elif isinstance(other, SelectedField{{name}}):
            set_field_f_sf_{{name}}(self, <SelectedField{{name}}>other)
        elif isinstance(other, SelectedPoints{{name}}):
            set_field_f_sp_{{name}}(self, <SelectedPoints{{name}}>other)
        else:
            raise Exception(f"Field{{name}} @= type mismatch {type(self)} {type(other)}")
        return self

    @q.timer
    def copy(self, cc.bool is_copying_data=True):
        f = type(self)()
        if is_copying_data:
            f @= self
        return f

    @q.timer
    def set_zero(self):
        cc.set_zero(self.xx)

    def swap(self, Field{{name}} f1):
        cc.qswap(f1.xx, self.xx)

    def total_site(self):
        cdef Coordinate x = Coordinate()
        cc.assign_direct(x.xx, self.xx.get_geo().total_site())
        return x

    def multiplicity(self):
        return self.xx.get_geo().multiplicity

    def sizeof_m(self):
        return self.ctype.size()

    def geo(self):
        cdef Geometry geo = Geometry()
        # cc.assign_direct(geo.xx, self.xx.get_geo())
        geo.xx = self.xx.get_geo()
        return geo

    def qnorm(self):
        return cc.qnorm(self.xx)

    def qnorm_field(self):
        cdef Geometry geo = self.geo()
        cdef FieldRealD f_n = FieldRealD(geo, 1)
        cc.qnorm_field(f_n.xx, self.xx)
        return f_n

    def glb_sum(self):
        """
        return SelectedPoints with n_points=1
        """
        from .c import get_psel_single
        cdef PointsSelection psel = get_psel_single(self.total_site())
        cdef SelectedPoints{{name}} sp = SelectedPoints{{name}}(psel, self.multiplicity())
        cc.field_glb_sum(sp.xx, self.xx)
        return sp

    def glb_sum_tslice(self, *, int t_dir=3):
        """
        return SelectedPoints with n_points=total_site[t_dir]
        """
        from .c import get_psel_tslice
        cdef PointsSelection psel = get_psel_tslice(self.total_site(), t_dir=t_dir)
        cdef SelectedPoints{{name}} sp = SelectedPoints{{name}}(psel, self.multiplicity())
        cc.field_glb_sum_tslice(sp.xx, self.xx, t_dir)
        return sp

    def get_elems_xg(self, xg_arr):
        cdef Geometry geo = self.geo()
        cdef PointsSelection psel = PointsSelection(xg_arr, geo)
        cdef SelectedPoints{{name}} sp = SelectedPoints{{name}}(psel, self.multiplicity())
        sp @= self
        return sp

    def set_elems_xg(self, xg_arr, val):
        cdef Geometry geo = self.geo()
        cdef PointsSelection psel = PointsSelection(xg_arr, geo)
        cdef SelectedPoints{{name}} sp = SelectedPoints{{name}}(psel, self.multiplicity())
        np.asarray(sp).ravel()[:] = np.asarray(val).ravel()
        self @= sp

    def get_elem_xg(self, xg_arr, int m):
        cdef Geometry geo = self.geo()
        cdef PointsSelection psel = PointsSelection(xg_arr, geo)
        cdef SelectedPoints{{name}} sp = SelectedPoints{{name}}(psel, 1)
        cc.set_selected_points(sp.xx, self.xx, psel.xx, m)
        return sp

    def set_elem_xg(self, xg_arr, int m, val):
        cdef Geometry geo = self.geo()
        cdef PointsSelection psel = PointsSelection(xg_arr, geo)
        cdef SelectedPoints{{name}} sp = SelectedPoints{{name}}(psel, 1)
        np.asarray(sp).ravel()[:] = np.asarray(val).ravel()
        cc.set_field_selected(self.xx, sp.xx, geo.xx, psel.xx, m)

    def shift(self, Coordinate shift not None):
        """
        return new shifted Field
        shift is the coordinate to shift the field
        """
        cdef Field{{name}} f1 = self.copy(is_copying_data=False)
        cc.field_shift(f1.xx, self.xx, shift.xx)
        return f1

    def reflect(self):
        """
        return new reflected field
        """
        cdef Field{{name}} f1 = self.copy()
        cc.reflect_field(f1.xx)
        return f1

    def read_direct(self, const cc.std_string& fn, Coordinate new_size_node=None):
        if new_size_node is None:
            new_size_node = Coordinate()
        return cc.read_field(self.xx, fn, new_size_node.xx)

    def write_direct(self, const cc.std_string& fn, Coordinate new_size_node=None):
        if new_size_node is None:
            new_size_node = Coordinate()
        return cc.write_field(self.xx, fn, new_size_node.xx)

    def read_sfr_direct(self, ShuffledFieldsReader sfr, const cc.std_string& fn):
        return cc.read(sfr.xx, fn, self.xx)

    def write_sfw_direct(self, ShuffledFieldsWriter sfw, const cc.std_string& fn):
        return cc.write(sfw.xx, fn, self.xx)

field_type_dict[ElemType{{name}}] = Field{{name}}

{{endfor}}

### -------------------------------------------------------------------
